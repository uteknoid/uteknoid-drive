apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

def commitSHA1 = 'COMMIT_SHA1'
def gitRemote = 'GIT_REMOTE'

dependencies {
    // Data and domain modules
    implementation project(':owncloudDomain')
    implementation project(':owncloudData')

    // Dependencies for app building
    implementation "androidx.legacy:legacy-support-v4:$androidX"
    implementation "androidx.annotation:annotation:1.3.0"
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation 'com.google.android.exoplayer:exoplayer:2.16.1'
    implementation 'com.github.aritraroy.PatternLockView:patternlockview:a90b0d4bf0'
    implementation "androidx.appcompat:appcompat:$appCompat"
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.sqlite:sqlite-ktx:$sqliteVersion"
    implementation "androidx.biometric:biometric:$biometricVersion"

    // Image loading
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    implementation 'com.github.corouteam:GlideToVectorYou:v2.0.0'

    // CustomTabs required for OAuth2 and OIDC.
    implementation "androidx.browser:browser:1.4.0"

    // Zooming Android ImageView.
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$archLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$archLifecycleVersion"
    kapt "androidx.lifecycle:lifecycle-common-java8:$archLifecycleVersion"

    implementation "androidx.room:room-runtime:$roomVersion"

    // Koin dependency injector
    implementation "io.insert-koin:koin-core:$koinVersion"
    implementation "io.insert-koin:koin-androidx-workmanager:$koinVersion"

    // WorkManager
    implementation "androidx.work:work-runtime-ktx:2.7.1"

    // KTX extensions, see https://developer.android.com/kotlin/ktx.html
    implementation "androidx.core:core-ktx:$ktxCoreVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$ktxViewModelVersion"
    implementation "androidx.fragment:fragment-ktx:$ktxFragmentVersion"

    // Preferences
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // MDM feedback
    implementation 'androidx.enterprise:enterprise-feedback:1.1.0'

    // Tests
    testImplementation project(':owncloudTestUtil')
    testImplementation "junit:junit:$junitVersion"
    testImplementation "androidx.arch.core:core-testing:$archLifecycleVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

    // Instrumented tests
    androidTestImplementation project(':owncloudTestUtil')
    androidTestImplementation "androidx.test:core:$androidTestVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestImplementation "androidx.test.ext:junit:$extJunitVersion"
    androidTestImplementation "androidx.test:core-ktx:$androidTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoTestVersion"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espressoTestVersion"
    androidTestImplementation "androidx.test.uiautomator:uiautomator:$uiAutomatorTestVersion"
    androidTestImplementation "androidx.annotation:annotation:$annotationTestVersion"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation("io.mockk:mockk-android:$mockkVersion") {
        exclude module: 'objenesis'
    }
    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'
    androidTestImplementation 'androidx.test:runner:1.3.0'

    debugImplementation "androidx.fragment:fragment-testing:1.4.1"
    debugImplementation 'com.facebook.stetho:stetho:1.6.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
}

android {
    compileSdkVersion sdkCompileVersion

    defaultConfig {
        minSdkVersion sdkMinVersion
        targetSdkVersion sdkTargetVersion

        testInstrumentationRunner "com.uteknoid.drive.utils.OCTestAndroidJUnitRunner"

        versionCode = 10000000
        versionName = "1.0.0"

        buildConfigField "String", gitRemote, "\"" + getGitOriginRemote() + "\""
        buildConfigField "String", commitSHA1, "\"" + getLatestGitHash() + "\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }

    lint {
        abortOnError true
        htmlOutput file('../lint-app-report.html')
        ignoreWarnings false
        xmlReport false
    }

    adbOptions {
        timeOutInMs(20 * 60 * 1000)
    }

    signingConfigs {
        release {
            if (System.env.OC_RELEASE_KEYSTORE) {
                storeFile file(System.env.OC_RELEASE_KEYSTORE)  // use an absolute path
                storePassword System.env.OC_RELEASE_KEYSTORE_PASSWORD
                keyAlias System.env.OC_RELEASE_KEY_ALIAS
                keyPassword System.env.OC_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {

        release {
            if (System.env.OC_RELEASE_KEYSTORE) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            applicationIdSuffix ""
        }
    }

    flavorDimensions "management"
    productFlavors {
        original {
            dimension "management"
        }
        mdm {
            dimension "management"
        }
    }

    applicationVariants.all { variant ->
        def appName = System.env.OC_APP_NAME
        setOutputFileName(variant, appName, project)
    }

    testOptions {
        unitTests.returnDefaultValues = true
        animationsDisabled = true
    }

    buildFeatures {
        viewBinding true
    }
}

// Updates output file names of a given variant to format
// [appName].[variant.versionName].[OC_BUILD_NUMBER]-[variant.name].apk.
//
// OC_BUILD_NUMBER is an environment variable read directly in this method. If undefined, it's not added.
//
// @param variant           Build variant instance which output file name will be updated.
// @param appName           String to use as first part of the new file name. May be undefined, the original
//                          project.archivesBaseName property will be used instead.
// @param callerProject     Caller project.

def setOutputFileName(variant, appName, callerProject) {
    logger.info("Setting new name for output of variant $variant.name")

    def originalFile = variant.outputs[0].outputFile
    def originalName = originalFile.name
    println "originalName is $originalName"

    def newName = "uteknoid"

    if (appName) {
        newName += appName
    } else {
        newName += "uteknoid"
    }

    def versionName = "$variant.mergedFlavor.versionName"
    if (variant.mergedFlavor.manifestPlaceholders.versionName != null) {
        versionName = "$variant.mergedFlavor.manifestPlaceholders.versionName"
    }
    if (variant.buildType.manifestPlaceholders.versionName != null) {
        versionName = "$variant.buildType.manifestPlaceholders.versionName"
    }
    newName += "_$versionName"

    def buildNumber = System.env.OC_BUILD_NUMBER
    if (buildNumber) {
        newName += "_$buildNumber"
    }

    newName += originalName.substring(callerProject.archivesBaseName.length())

    println "$variant.name: newName is $newName"

    variant.outputs.all {
        outputFileName = new File(".", newName)
    }
}

static def getLatestGitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.toString().trim()
}

static def getGitOriginRemote() {
    def process = "git remote -v".execute()
    def values = process.text.toString().trim().split("\\r\\n|\\n|\\r")

    def found = values.find { it.startsWith("origin") && it.endsWith("(push)") }
    return found.replace("origin", "").replace("(push)", "").replace(".git", "").trim()
}
